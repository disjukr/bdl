# standard - conventional

struct BdlIr {
  modules: Module[string], // key: module path (bdl.ast, bdl.ir, ...)
  defs: Def[string], // key: def path (bdl.ast.BdlAst, bdl.ir.BdlIr, ...)
}

struct Module {
  fileUrl?: string,
  attributes: string[string],
  defPaths: string[],
  imports: Import[],
}

struct Import {
  attributes: string[string],
  modulePath: string,
  items: ImportItem[],
}

struct ImportItem {
  name: string,
  as?: string,
}

union Def {
  Custom(
    attributes: string[string],
    name: string,
    originalType: Type,
  ),
  Enum(
    attributes: string[string],
    name: string,
    items: EnumItem[],
  ),
  Oneof(
    attributes: string[string],
    name: string,
    items: OneofItem[],
  ),
  Proc(
    attributes: string[string],
    name: string,
    inputType: Type,
    outputType: Type,
    errorType?: Type,
  ),
  Struct(
    attributes: string[string],
    name: string,
    fields: StructField[],
  ),
  Union(
    attributes: string[string],
    name: string,
    items: UnionItem[],
  ),
}

struct EnumItem {
  attributes: string[string],
  name: string,
}

struct OneofItem {
  attributes: string[string],
  itemType: Type,
}

struct StructField {
  attributes: string[string],
  name: string,
  fieldType: Type,
  optional: boolean,
}

union Type {
  Plain(valueTypePath: string),
  Array(valueTypePath: string),
  Dictionary(valueTypePath: string, keyTypePath: string),
}

struct UnionItem {
  attributes: string[string],
  name: string,
  fields: StructField[],
}
